buildscript {
    repositories {
        maven { url = 'https://files.minecraftforge.net/maven' }
        jcenter()
        mavenCentral()
    }
    dependencies {
        classpath group: 'net.minecraftforge.gradle', name: 'ForgeGradle', version: '3.+', changing: true
    }
}
apply plugin: 'net.minecraftforge.gradle'
apply plugin: 'eclipse'

version = "1.14.4-${verspec}.${verbuild}"
group = "me.towdium.jecharacters"
archivesBaseName = "jecharacters"

sourceCompatibility = targetCompatibility = '1.8'

repositories {
    mavenCentral()
    maven { url = 'http://dvs1.progwml6.com/files/maven' }
    flatDir { dirs 'libs' }
}

configurations {
    fatJar
}

minecraft {
    mappings channel: 'snapshot', version: '20190719-1.14.3'
    runs {
        client {
            workingDirectory project.file('run')
            property 'forge.logging.console.level', 'debug'
            mods {
                jecharacters {
                    source sourceSets.main
                }
            }
        }

        server {
            workingDirectory project.file('run')
            property 'forge.logging.console.level', 'debug'
            mods {
                jecharacters {
                    source sourceSets.main
                }
            }
        }

        data {
            workingDirectory project.file('run')
            property 'forge.logging.console.level', 'debug'
            args '--mod', 'jecharacters', '--all', '--output', file('src/generated/resources/')
            mods {
                jecharacters {
                    source sourceSets.main
                }
            }
        }
    }
}

clean {
    delete 'libs'
}

dependencies {
    mkdir 'libs'
    def url = 'https://github.com/Towdium/PinIn/releases/' +
            "download/v${verpinin}/PinIn-${verpinin}.jar"
    ant.get src: url, dest: "libs/PinIn-${verpinin}.jar", skipexisting: true
    implementation name: "PinIn-${verpinin}"
    fatJar name: "PinIn-${verpinin}"

    minecraft 'net.minecraftforge:forge:1.14.4-28.1.107'
    implementation fg.deobf('mezz.jei:jei-1.14.4:6.0.0.13')
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.2.0'
}

test {
    useJUnitPlatform()
}

jar {
    manifest {
        attributes([
                "Specification-Title": 'Just Enough Characters',
                "Specification-Vendor": 'Towdium',
                "Specification-Version": "${verspec}",
                "Implementation-Title": project.name,
                "Implementation-Version": "${verspec}.${verbuild}",
                "Implementation-Vendor" : 'Towdium',
                "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
        ])
    }

    from {
        configurations.fatJar.collect { it.isDirectory() ? it : zipTree(it) }
    }
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

task generate(type:Exec) {
    commandLine 'python', 'generate.py'
}

compileJava.dependsOn generate
